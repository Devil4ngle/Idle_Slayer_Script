#NoTrayIcon
#Region ;**** Directives created by AutoIt3Wrapper_GUI ****
#AutoIt3Wrapper_Icon=Resources\Icon.ico
#AutoIt3Wrapper_Outfile=D:\Idle Macro\idleslayer\Idle Runner_x32.exe
#AutoIt3Wrapper_Outfile_x64=D:\Idle Macro\idleslayer\Idle Runner_x64.exe
#AutoIt3Wrapper_Compression=0
#AutoIt3Wrapper_Compile_Both=y
#AutoIt3Wrapper_UseX64=y
#AutoIt3Wrapper_Res_requestedExecutionLevel=None
#AutoIt3Wrapper_Res_File_Add=Resources\Icon.jpg, RT_RCDATA, ICON,0
#AutoIt3Wrapper_Res_File_Add=Resources\Welcome.jpg, RT_RCDATA, WELCOME,0
#AutoIt3Wrapper_Res_File_Add=Resources\Instructions.jpg, RT_RCDATA, INSTRUCTION,0
#AutoIt3Wrapper_Res_File_Add=Resources\CheckboxUnchecked.jpg, RT_RCDATA, UNCHECKED,0
#AutoIt3Wrapper_Res_File_Add=Resources\CheckboxChecked.jpg, RT_RCDATA, CHECKED,0
#AutoIt3Wrapper_Res_File_Add=Resources\CraftRagePill.jpg, RT_RCDATA, RAGEPILL,0
#AutoIt3Wrapper_Res_File_Add=Resources\CraftSoulBonus.jpg, RT_RCDATA, SOULBONUS,0
#AutoIt3Wrapper_Res_File_Add=Resources\AutoBuyUpgrades.jpg, RT_RCDATA, AUTOUPGRADES,0
#AutoIt3Wrapper_Res_File_Add=Resources\CirclePortals.jpg, RT_RCDATA, CIRCLEPORTALS,0
#AutoIt3Wrapper_Res_File_Add=Resources\SkipBonusStage.jpg, RT_RCDATA, SKIPBONUS,0
#AutoIt3Wrapper_Res_File_Add=Resources\Home.jpg, RT_RCDATA, HOME,0
#AutoIt3Wrapper_Res_File_Add=Resources\General.jpg, RT_RCDATA, GENERAL,0
#AutoIt3Wrapper_Res_File_Add=Resources\Minigames.jpg, RT_RCDATA, MINIGAMES,0
#AutoIt3Wrapper_Res_File_Add=Resources\Log.jpg, RT_RCDATA, LOG,0
#AutoIt3Wrapper_Res_File_Add=Resources\Stop.jpg, RT_RCDATA, STOP,0
#AutoIt3Wrapper_Res_File_Add=Resources\Start.jpg, RT_RCDATA, START,0
#AutoIt3Wrapper_Res_File_Add=Resources\Exit.jpg, RT_RCDATA, EXIT,0
#AutoIt3Wrapper_Res_File_Add=Resources\Crafting.jpg, RT_RCDATA, CRAFTING,0
#AutoIt3Wrapper_Res_File_Add=Resources\Github.jpg, RT_RCDATA, GITHUB,0
#AutoIt3Wrapper_Res_File_Add=Resources\Update.jpg, RT_RCDATA, UPDATE,0
#AutoIt3Wrapper_Res_File_Add=Resources\JumpRate.jpg, RT_RCDATA, JUMPRATE,0
#AutoIt3Wrapper_Res_File_Add=Resources\AutoAscend.jpg, RT_RCDATA, AUTOASCEND,0
#AutoIt3Wrapper_Res_File_Add=Resources\UpArrow.jpg, RT_RCDATA, UPARROW,0
#AutoIt3Wrapper_Res_File_Add=Resources\DownArrow.jpg, RT_RCDATA, DOWNARROW,0
#AutoIt3Wrapper_Res_File_Add=Resources\NoLockpicking.jpg, RT_RCDATA, NOLOCKPICKING,0
#AutoIt3Wrapper_Res_File_Add=Resources\NoReinforcedCrystalSaver.jpg, RT_RCDATA, NOREINFORCEDCRYSTALSAVER,0
#AutoIt3Wrapper_Res_File_Add=Resources\CraftBidimensionalStaff.jpg, RT_RCDATA, BIDIMENSIONAL,0
#AutoIt3Wrapper_Res_File_Add=Resources\CraftDimensionalStaff.jpg, RT_RCDATA, DIMENSIONAL,0
#AutoIt3Wrapper_Res_File_Add=Resources\PerfectChestHunt.jpg, RT_RCDATA, PERFECTCHESTHUNT,0
#AutoIt3Wrapper_Res_File_Add=Resources\DisableRage.jpg, RT_RCDATA, DISABLERAGE,0
#AutoIt3Wrapper_Res_File_Add=Resources\0.jpg, RT_RCDATA, NUM0,0
#AutoIt3Wrapper_Res_File_Add=Resources\10.jpg, RT_RCDATA, NUM10,0
#AutoIt3Wrapper_Res_File_Add=Resources\20.jpg, RT_RCDATA, NUM20,0
#AutoIt3Wrapper_Res_File_Add=Resources\30.jpg, RT_RCDATA, NUM30,0
#AutoIt3Wrapper_Res_File_Add=Resources\40.jpg, RT_RCDATA, NUM40,0
#AutoIt3Wrapper_Res_File_Add=Resources\50.jpg, RT_RCDATA, NUM50,0
#AutoIt3Wrapper_Res_File_Add=Resources\60.jpg, RT_RCDATA, NUM60,0
#AutoIt3Wrapper_Res_File_Add=Resources\70.jpg, RT_RCDATA, NUM70,0
#AutoIt3Wrapper_Res_File_Add=Resources\80.jpg, RT_RCDATA, NUM80,0
#AutoIt3Wrapper_Res_File_Add=Resources\90.jpg, RT_RCDATA, NUM90,0
#AutoIt3Wrapper_Res_File_Add=Resources\100.jpg, RT_RCDATA, NUM100,0
#AutoIt3Wrapper_Res_File_Add=Resources\110.jpg, RT_RCDATA, NUM110,0
#AutoIt3Wrapper_Res_File_Add=Resources\120.jpg, RT_RCDATA, NUM120,0
#AutoIt3Wrapper_Res_File_Add=Resources\130.jpg, RT_RCDATA, NUM130,0
#AutoIt3Wrapper_Res_File_Add=Resources\140.jpg, RT_RCDATA, NUM140,0
#AutoIt3Wrapper_Res_File_Add=Resources\150.jpg, RT_RCDATA, NUM150,0
#AutoIt3Wrapper_Res_File_Add=Resources\160.jpg, RT_RCDATA, NUM160,0
#AutoIt3Wrapper_Res_File_Add=Resources\170.jpg, RT_RCDATA, NUM170,0
#AutoIt3Wrapper_Res_File_Add=Resources\180.jpg, RT_RCDATA, NUM180,0
#AutoIt3Wrapper_Res_File_Add=Resources\190.jpg, RT_RCDATA, NUM190,0
#AutoIt3Wrapper_Res_File_Add=Resources\200.jpg, RT_RCDATA, NUM200,0
#AutoIt3Wrapper_Res_File_Add=Resources\210.jpg, RT_RCDATA, NUM210,0
#AutoIt3Wrapper_Res_File_Add=Resources\220.jpg, RT_RCDATA, NUM220,0
#AutoIt3Wrapper_Res_File_Add=Resources\230.jpg, RT_RCDATA, NUM230,0
#AutoIt3Wrapper_Res_File_Add=Resources\240.jpg, RT_RCDATA, NUM240,0
#AutoIt3Wrapper_Res_File_Add=Resources\250.jpg, RT_RCDATA, NUM250,0
#AutoIt3Wrapper_Res_File_Add=Resources\260.jpg, RT_RCDATA, NUM260,0
#AutoIt3Wrapper_Res_File_Add=Resources\270.jpg, RT_RCDATA, NUM270,0
#AutoIt3Wrapper_Res_File_Add=Resources\280.jpg, RT_RCDATA, NUM280,0
#AutoIt3Wrapper_Res_File_Add=Resources\290.jpg, RT_RCDATA, NUM290,0
#AutoIt3Wrapper_Res_File_Add=Resources\300.jpg, RT_RCDATA, NUM300,0
#AutoIt3Wrapper_Run_Stop_OnError=y
#AutoIt3Wrapper_Run_Au3Stripper=y
#EndRegion ;**** Directives created by AutoIt3Wrapper_GUI ****
#comments-start
 AutoIt Version: 3.3.16.0
 Author: Devil4ngle, Djahnz
#comments-end
#include-once
#include "Libraries\ResourcesEx.au3"
#include "Libraries\GUI.au3"
#include "Libraries\BonusStage.au3"
#include "Libraries\BossFightVictor.au3"
#include "Libraries\BossFightKnight.au3"
#include "Libraries\Common.au3"
#include "Libraries\AscendingHeights.au3"
#include "Libraries\Chesthunt.au3"
#include <ButtonConstants.au3>
#include <GUIConstantsEx.au3>
#include <StaticConstants.au3>
#include <TabConstants.au3>
#include <WindowsConstants.au3>
#include <GuiTab.au3>
#include <WinAPI.au3>
#include <WinAPISysWin.au3>
#include <EditConstants.au3>
#include <AutoItConstants.au3>
#include <Array.au3>

setSetting()
_AuThread_Startup()
Main()

Func Main()
	; Set Hotkey Bindings
	HotKeySet("{Home}", "Pause")
	HotKeySet("+{Esc}", "IdleClose")
	HotKeySet("^+b", "BuyEquipment")
	; Create Saving Directory
	DirCreate("IdleRunnerLogs")
	; Create GUI
	CreateGUI()
	LoadSettings()
	GUISetState(@SW_SHOW)
	; A lot of Global Function are declared in Libraries/GUI
	_AuThread_StartThread("ShootAndBoost", @AutoItPID)
	SyncProcess()

	; Infinite Loops
	While 1
		Sleep(40)

		If $bTogglePause Then ContinueLoop

		If (1800000 < TimerDiff($iTimerFocusGame)) Then
			$iTimerFocusGame = TimerInit()
			WinActive("Idle Slayer")
			ControlFocus("Idle Slayer", "", "")
		EndIf

		; Silver box collect
		PixelSearch(650, 36, 650, 36, 0xCA9700)
		If Not @error Then
			WriteInLogs("Silver Box Collected")
			MouseClick("left", 644, 49, 1, 0)
		EndIf

		; Rage when Megahorde
		PixelSearch(419, 323, 419, 323, 0xDFDEE0)
		If Not @error Then
			SyncProcess(False)
			RageWhenHorde()
			SyncProcess(True)
		EndIf

		; Claim quests
		PixelSearch(1130, 610, 1130, 610, 0xCBCB4C)
		If Not @error Then
			SyncProcess(False)
			ClaimQuests()
			SyncProcess(True)
		EndIf

		; Rage when Soul Bonus
		PixelSearch(625, 143, 629, 214, 0xA86D0A)
		If Not @error Then
			ControlSend("Idle Slayer", "", "", "{r}")
		EndIf

		; Collect minions
		PixelSearch(99, 113, 99, 113, 0xFFFF7A)
		If Not @error Then
			SyncProcess(False)
			CollectMinion()
			SyncProcess(True)
		EndIf


		; Chest-hunt
		PixelSearch(187, 296, 187, 296, 0xFFBB31)
		If Not @error Then
			PixelSearch(187, 303, 187, 303, 0xF68F37)
			If Not @error Then
				SyncProcess(False)
				Chesthunt($bNoLockpickingState, $bPerfectChestHuntState, $bNoReinforcedCrystalSaverState)
				SyncProcess(True)
			EndIf

		EndIf

		If TimerDiff($iLastCheckTimeLoop) >= 5000 Then
			$iLastCheckTimeLoop = TimerInit()

			CloseAll()

			; Bonus stage
			PixelSearch(660, 254, 660, 254, 0xFFE737)
			If Not @error Then
				PixelSearch(638, 236, 638, 236, 0xFFBB31)
				If Not @error Then
					PixelSearch(775, 448, 775, 448, 0xFFFFFF)
					If Not @error Then
						SyncProcess(False)
						BonusStage($bSkipBonusStageState)
						SyncProcess(True)
					EndIf
				EndIf
			EndIf

			; Boss Fight
			PixelSearch(639, 224, 639, 224, 0xFF878A)
			If Not @error Then
				PixelSearch(634, 224, 634, 224, 0xF263BD)
				If Not @error Then
					PixelSearch(644, 224, 644, 224, 0xFFF38F)
					If Not @error Then
						SyncProcess(False)
						PixelSearch(30, 690, 30, 690, 0x0B0303)
						If Not @error Then
							BossFightKnight()
						Else
							BossFightVictor()
						EndIf
						SyncProcess(True)
					EndIf
				EndIf
			EndIf

			; Ascending Heights
			PixelSearch(671, 213, 671, 213, 0xC2F4F9)
			If Not @error Then
				PixelSearch(640, 240, 634, 640, 0xFFCC66)
				If Not @error Then
					SyncProcess(False)
					AscendingHeights()
					SyncProcess(True)
				EndIf
			EndIf

			; Circle portal
			If $bCirclePortalsState Then
				CirclePortals()
			EndIf

			; Auto buy upgrades
			If $bAutoBuyUpgradeState Then
				If (($iAutoBuyTempTimer * 60000) < TimerDiff($iTimerAutoBuy)) Then
					$iTimerAutoBuy = TimerInit()
					$iAutoBuyTempTimer = $iAutoBuyTimer
					WinActivate("Idle Slayer")
					If WinGetTitle("[ACTIVE]") == "Idle Slayer" Then
						SyncProcess(False)
						$iAutoBuyLoopAmount = 0
						BuyEquipment()
						SyncProcess(True)
					EndIf
				EndIf
			EndIf

			; Auto Ascend
			If $bAutoAscendState Then
				If (($iAutoAscendTimer * 60000) < TimerDiff($iTimerAutoAscend)) Then
					$iTimerAutoAscend = TimerInit()
					WinActivate("Idle Slayer")
					If WinGetTitle("[ACTIVE]") == "Idle Slayer" Then
						SyncProcess(False)
						AutoAscend()
						SyncProcess(True)
					EndIf
				EndIf
			EndIf
		EndIf
	WEnd
EndFunc   ;==>Main

Func CloseAll()
	Sleep(2000)
	PixelSearch(680, 593, 680, 593, 0xAF0000)
	If Not @error Then
		MouseClick("left", 780, 600, 1, 0)
	EndIf
EndFunc   ;==>CloseAll

Func RageWhenHorde()
	Local $bSoulBonusActive = CheckForSoulBonus()
	If $bSoulBonusActive Then
		If $bCraftRagePillState Then
			BuyTempItem("0x871646")
		EndIf
		If $bCraftSoulBonusState Then
			BuyTempItem("0x7D55D8")
		EndIf
	EndIf
	If $bDisableRageState Then
		If $bSoulBonusActive Then
			Rage()
		EndIf
	Else
		Rage()
	EndIf

EndFunc   ;==>RageWhenHorde

Func Rage()
	WriteInLogs("MegaHorde Rage")
	If $bDimensionalState Then
		BuyTempItem("0xF37C55")
		$bDimensionalState = False
		If Not @Compiled Then
			GUICtrlSetImage($iCheckBoxbDimensionalState, 'Resources\CheckboxUnchecked.jpg')
		Else
			_Resource_SetToCtrlID($iCheckBoxbDimensionalState, 'UNCHECKED')
		EndIf
		SaveSettings()
	EndIf
	If $bBiDimensionalState Then
		BuyTempItem("0x526629")
		$bBiDimensionalState = False
		If Not @Compiled Then
			GUICtrlSetImage($iCheckBoxbDimensionalState, 'Resources\CheckboxUnchecked.jpg')
		Else
			_Resource_SetToCtrlID($iCheckBoxbBiDimensionalState, 'UNCHECKED')
		EndIf
		SaveSettings()
	EndIf
	ControlFocus("Idle Slayer", "", "")
	ControlSend("Idle Slayer", "", "", "{r}")
EndFunc   ;==>Rage

Func CheckForSoulBonus()
	PixelSearch(625, 143, 629, 214, 0xA86D0A)
	If Not @error Then
		WriteInLogs("MegaHorde Rage with SoulBonus")
		Return True
	EndIf
	Return False
EndFunc   ;==>CheckForSoulBonus

Func BuyTempItem($sHexColor)
	WriteInLogs("Trying to CraftingTemp Item")
	Local $aFoundPixel
	;open menu
	MouseClick("left", 160, 100, 1, 0)
	Sleep(150)
	;temp item
	MouseClick("left", 260, 690, 1, 0)
	Sleep(150)

	$aFoundPixel = PixelSearch(43, 330, 625, 630, $sHexColor)
	If Not @error Then
		MouseClick("left", $aFoundPixel[0], $aFoundPixel[1], 1, 0)
		Sleep(200)
		MouseClick("left", 407, 213, 1, 0)
		WriteInLogs("CraftingTemp Item Active")
	Else
		WriteInLogs("CraftingTemp Item Failed, not enough materials")
	EndIf
	; Close
	MouseClick("left", 440, 690, 1, 0)
	Sleep(100)
EndFunc   ;==>BuyTempItem

Func AutoAscend()
	PixelSearch(260, 600, 260, 600, 0x56188B)
	If Not @error Then
		MouseClick("left", 260, 600, 1, 0)
		Sleep(300)
		MouseClick("left", 550, 580, 1, 0)
		Sleep(300)
		BuyEquipment()
	Else
		;Click ascension button
		MouseClick("left", 95, 90, 1, 0)
		Sleep(400)
		;Click ascension tab
		MouseClick("left", 93, 680, 1, 0)
		Sleep(400)
		PixelSearch(260, 600, 260, 600, 0x56188B)
		If Not @error Then
			MouseClick("left", 260, 600, 1, 0)
			Sleep(300)
			MouseClick("left", 550, 580, 1, 0)
			Sleep(300)
			BuyEquipment()
		EndIf
	EndIf
EndFunc   ;==>AutoAscend

Func CollectMinion()
	;Click ascension button
	MouseClick("left", 95, 90, 1, 0)
	Sleep(400)
	;Click ascension tab
	MouseClick("left", 93, 680, 1, 0)
	Sleep(200)
	;Click ascension tree tab
	MouseClick("left", 193, 680, 1, 0)
	Sleep(200)
	;????
	MouseClick("left", 691, 680, 1, 0)
	Sleep(200)
	;Click minion tab
	MouseClick("left", 332, 680, 1, 0)
	Sleep(200)

	;Check if Daily Bonus is available
	PixelSearch(370, 410, 910, 470, 0x11AA23, 9)
	If Not @error Then
		;Click Claim All
		MouseClick("left", 320, 280, 5, 0)
		Sleep(200)
		;Click Send All
		MouseClick("left", 320, 280, 5, 0)
		Sleep(200)
		;Claim Daily Bonus
		MouseClick("left", 320, 180, 5, 0)
		Sleep(200)
		WriteInLogs("Minions Collect with Daily Bonus")
	Else
		;Click Claim All
		MouseClick("left", 318, 182, 5, 0)
		Sleep(200)
		;Click Send All
		MouseClick("left", 318, 182, 5, 0)
		Sleep(200)
		WriteInLogs("Minions Collect")
	EndIf

	;Click Exit
	MouseClick("left", 570, 694, 1, 0)
EndFunc   ;==>CollectMinion

Func CirclePortals()
	;Check if portal button is visible
	Local $iPortalVisible = 0
	PixelSearch(1180, 180, 1180, 180, 0x830399)
	If @error Then
		$iPortalVisible += 1
	EndIf
	PixelSearch(1180, 180, 1180, 180, 0x290130)
	If @error Then
		$iPortalVisible += 1
	EndIf

	If $iPortalVisible == 2 Then
		Return
	EndIf

	;Check if timer is up
	PixelSearch(1154, 144, 1210, 155, 0xFFFFFF, 9)
	If @error Then
		SyncProcess(False)
		;Click portal button
		MouseClick("left", 1180, 150, 1, 0)
		Sleep(300)

		;Select destination
		;Top of scrollbar
		MouseMove(867, 300, 0)
		Sleep(200)
		Do
			MouseWheel($MOUSE_WHEEL_UP, 20)
			;Top of searchbar
			PixelSearch(875, 275, 875, 275, 0xD6D6D6)
		Until @error
		Sleep(400)

		Local $sColor = 0xFFFFFF
		Switch $iCirclePortalsCount
			Case 1
				$sColor = 0x6FF5F8
			Case 2
				$sColor = 0x00CBF8
			Case 3
				$sColor = 0x009D93
			Case 4
				$sColor = 0x000178
			Case 5
				$sColor = 0xE198BF
			Case 6
				$sColor = 0x00F8B5
			Case 7
				$sColor = 0xB362C7
			Case 8
				$sColor = 0xC5464B
			Case 9
				$sColor = 0x4F0085
		EndSwitch
		Local $aLocation
		While 1
			$aLocation = PixelSearch(491, 266, 491, 540, $sColor)
			If @error Then
				;Check gray scroll bar is there
				PixelSearch(875, 536, 875, 536, 0xD6D6D6)
				If @error Then
					ExitLoop
				EndIf
				Sleep(10)
				;Move mouse on ScrollBar
				MouseMove(867, 300, 0)
				MouseWheel($MOUSE_WHEEL_DOWN, 1)
			Else
				;Click portal
				MouseClick("left", $aLocation[0], $aLocation[1], 1, 0)
				Sleep(300)
				ExitLoop
			EndIf
		WEnd

		$iCirclePortalsCount += 1
		If $iCirclePortalsCount > 9 Then
			$iCirclePortalsCount = 1
		EndIf
		SaveSettings()
		WriteInLogs("CirclePortals")
		Sleep(10000)
		SyncProcess(True)
	EndIf
EndFunc   ;==>CirclePortals

Func BuyEquipment()
	WriteInLogs("AutoUpgrade Active")
	;Close Shop window if open
	MouseClick("left", 1244, 712, 1, 0)
	Sleep(150)
	;Open shop window
	MouseClick("left", 1163, 655, 1, 0)
	Sleep(150)
	PixelSearch(807, 142, 807, 142, 0xFFFFFF)
	If Not @error Then
		;Click on armor tab
		MouseClick("left", 850, 690, 1, 0)
		Sleep(50)
		;Click Max buy
		MouseClick("left", 1180, 636, 4, 0)
		;Check if scrollbar is here if no max buy first item otherwise last item
		PixelSearch(1257, 340, 1257, 340, 0x11AA23)
		If Not @error Then
			;buy sword
			MouseClick("left", 1200, 200, 5, 0)
		Else
			;Click Bottom of scroll bar
			MouseClick("left", 1253, 592, 5, 0)
			Sleep(200)
		EndIf
		Local $aLocation
		While 1
			;Check if there is any green buy boxes
			$aLocation = PixelSearch(1160, 590, 1160, 170, 0x11AA23, 10)
			If @error Then
				;Move mouse on ScrollBar
				MouseMove(1253, 170, 0)
				MouseWheel($MOUSE_WHEEL_UP, 1)
				;Check gray scroll bar is there
				PixelSearch(1253, 168, 1253, 168, 0xD6D6D6)
				If @error Then
					ExitLoop
				EndIf
				Sleep(10)
			Else
				;Click on armor tab
				MouseClick("left", 850, 690, 1, 0)
				;Click Green buy box
				MouseClick("left", $aLocation[0], $aLocation[1], 5, 0)
			EndIf
		WEnd
		BuyUpgrade()
	EndIf
EndFunc   ;==>BuyEquipment

Func BuyUpgrade()
	; Navigate to upgrade and scroll up
	MouseClick("left", 927, 683, 1, 0)
	Sleep(150)
	; Top of scrollbar
	MouseMove(1254, 172, 0)
	Do
		MouseWheel($MOUSE_WHEEL_UP, 20)
		;Top of searchbar
		PixelSearch(1254, 167, 1254, 167, 0xD6D6D6)
	Until @error
	Sleep(400)
	Local $bSomethingBought = False
	Local $iY = 170
	While 1
		; Check if RandomBox Magnet is next upgrade
		PixelSearch(882, $iY, 909, $iY + 72, 0xF4B41B)
		If Not @error Then
			$iY += 96
		EndIf
		; Check if RandomBox Magnet is next upgrade
		PixelSearch(882, $iY, 909, $iY + 72, 0xE478FF)
		If Not @error Then
			$iY += 96
		EndIf
		;Electric worm
		;PixelSearch(850, $iY, 850, $iY + 72, 0xF7A01E)
		;If Not @error Then
		;	$iY += 96
		;EndIf
		PixelSearch(1180, $iY, 1180, $iY, 0x10A322, 9)
		If @error Then
			ExitLoop
		Else
			$bSomethingBought = True
			; Click green buy
			MouseClick("left", 1180, $iY, 1, 0)
			Sleep(50)
		EndIf
	WEnd
	If $bSomethingBought And $iAutoBuyLoopAmount < 5 Then
		$iAutoBuyLoopAmount += 1
		BuyEquipment()
	Else
		MouseClick("left", 1222, 677, 1, 0)
	EndIf
EndFunc   ;==>BuyUpgrade

Func ClaimQuests()
	WriteInLogs("Claiming quest")
	;Close Shop window if open
	MouseClick("left", 1244, 712, 1, 0)
	Sleep(150)
	;Open shop window
	MouseClick("left", 1163, 655, 1, 0)
	Sleep(150)
	;Click on armor tab
	MouseClick("left", 850, 690, 1, 0)
	;Click on upgrade tab
	MouseClick("left", 927, 683, 1, 0)
	Sleep(150)
	;Click on quest tab
	MouseClick("left", 1000, 690, 1, 0)
	Sleep(50)

	; Top of scrollbar
	MouseMove(1254, 272, 0)
	Do
		MouseWheel($MOUSE_WHEEL_UP, 20)
		;Top of searchbar
		Sleep(20)
		PixelSearch(1254, 267, 1254, 267, 0xD6D6D6)
	Until @error
	Sleep(400)

	While 1
		;Check if there is any green buy boxes
		$aLocation = PixelSearch(1160, 270, 1160, 590, 0x11AA23, 10)
		If @error Then
			;Move mouse on ScrollBar
			MouseMove(1253, 270, 0)
			MouseWheel($MOUSE_WHEEL_DOWN, 1)
			;Check gray scroll bar is there
			PixelSearch(1253, 645, 1253, 645, 0xD6D6D6)
			If @error Then
				ExitLoop
			EndIf
			Sleep(10)
		Else
			;Click Green buy box
			WriteInLogs("Quest Claimed")
			MouseClick("left", $aLocation[0], $aLocation[1], 5, 0)
		EndIf
	WEnd

	;Close Shop
	MouseClick("left", 1244, 712, 1, 0)

EndFunc   ;==>ClaimQuests



Func ShootAndBoost()
	Local $bJumpState = True
	Local $iJumpSliderValue = 0
	Local $iReadMsg = 0

	While True

		If TimerDiff($iReadMsg) > 700 Then
			; Read the message
			$sPendingMsg = _AuThread_ReadNewMsg()
			; Check if message is not empty
			If UBound($sPendingMsg) > 0 Then
				; Parse the message
				For $i = 0 To UBound($sPendingMsg) - 1
					$msg = $sPendingMsg[$i]
					$msgArray = StringSplit($msg, ";")
					For $j = 1 To $msgArray[0]
						$msgItem = StringSplit($msgArray[$j], ":")
						If $msgItem[1] = "JumpSliderValue" Then
							$iJumpSliderValue = Int($msgItem[2])
						ElseIf $msgItem[1] = "JumpState" Then
							$bJumpState = ($msgItem[2] = "True")
						EndIf
					Next
				Next
			EndIf
			$iReadMsg = TimerInit()
		EndIf

		While $bJumpState == False
			Sleep(700)
			If WinGetState("Idle Runner") == 0 Then
				Exit
			EndIf
			$sPendingMsg = _AuThread_ReadNewMsg()
			; Check if message is not empty
			If UBound($sPendingMsg) > 0 Then
				; Parse the message
				For $i = 0 To UBound($sPendingMsg) - 1
					$msg = $sPendingMsg[$i]
					$msgArray = StringSplit($msg, ";")
					For $j = 1 To $msgArray[0]
						$msgItem = StringSplit($msgArray[$j], ":")
						If $msgItem[1] = "JumpSliderValue" Then
							$iJumpSliderValue = Int($msgItem[2])
						ElseIf $msgItem[1] = "JumpState" Then
							$bJumpState = ($msgItem[2] = "True")
						EndIf
					Next
				Next
			EndIf
		WEnd

		If WinGetState("Idle Runner") == 0 Then
			Exit
		EndIf

		Sleep($iJumpSliderValue)
		If WinGetTitle("[ACTIVE]") <> "Idle Runner" Then
			ControlFocus("Idle Slayer", "", "")
		EndIf
		ControlFocus("Idle Slayer", "", "")
		ControlSend("Idle Slayer", "", "", "{Up}{Right}")
	WEnd
EndFunc   ;==>ShootAndBoost
